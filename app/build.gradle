// android
apply plugin: 'com.android.application'
// kotlin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId 'de.salomax.ndx'
        minSdkVersion 24
        targetSdkVersion 29
        buildToolsVersion '28.0.3'
        // SemVer
        def major = 3
        def minor = 0
        def patch = 1
        versionCode = (major * 10000) + (minor * 100) + patch
        versionName = "$major ($major.$minor.$patch)"
    }

    signingConfigs {
        release {
            storeFile     file(getSecret('KEYSTORE_FILE'))
            storePassword getSecret('KEYSTORE_PASSWORD')
            keyAlias      getSecret('KEYSTORE_KEY_ALIAS')
            keyPassword   getSecret('KEYSTORE_KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' [DEBUG]'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning', 'GoogleAppIndexingApiWarning' // app doesn't provide deep-links
        disable 'UnusedResources' //like "text_resultSmall"
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    buildToolsVersion = '28.0.3'

    android {
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // ViewModel and LiveData
    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version" // optional

    // Room
    def room_version = '2.2.5'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // SupportLibs
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'

    // Widgets
    implementation 'com.github.guilhe:circular-progress-view:1.4.0'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'

    // billing
    implementation 'com.android.billingclient:billing:2.1.0'
}

def getSecret(String key) {
    def secretsFile = rootProject.file('secrets.properties')
    if (secretsFile.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(secretsFile))
        return props[key]
    } else {
        return null
    }
}
