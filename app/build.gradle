// android
apply plugin: 'com.android.application'
// kotlin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion = '30.0.3'

    defaultConfig {
        applicationId 'de.salomax.ndx'
        minSdkVersion 24
        targetSdkVersion 30
        // SemVer
        def major = 3
        def minor = 0
        def patch = 7
        versionCode = (major * 10000) + (minor * 100) + patch
        versionName = "$major.$minor.$patch"
        archivesBaseName = "$applicationId-v$versionCode"
    }

    signingConfigs {
        release {
            if (getSecret('KEYSTORE_FILE') != null) {
                storeFile file(getSecret('KEYSTORE_FILE'))
                storePassword getSecret('KEYSTORE_PASSWORD')
                keyAlias getSecret('KEYSTORE_KEY_ALIAS')
                keyPassword getSecret('KEYSTORE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            jniDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.release
            versionNameSuffix ' [DEBUG]'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning', 'GoogleAppIndexingApiWarning' // app doesn't provide deep-links
        disable 'UnusedResources' //like "text_resultSmall"
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    android {
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            debug.java.srcDirs += 'src/debug/kotlin'
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // ViewModel and LiveData
    def lifecycle_version = '2.3.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version" // optional

    // Room
    def room_version = '2.2.6'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // SupportLibs
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.3.0'

    // Widgets
    implementation 'com.github.guilhe:circular-progress-view:1.4.2'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'

    // billing
    implementation 'com.android.billingclient:billing-ktx:3.0.3'
}

def getSecret(String key) {
    def secretsFile = rootProject.file('secrets.properties')
    if (secretsFile.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(secretsFile))
        return props[key]
    } else {
        return null
    }
}
