apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId 'de.salomax.ndx'
        minSdkVersion 21
        targetSdkVersion 28
        buildToolsVersion '28.0.2'
        // SemVer
        def major = 3
        def minor = 0
        def patch = 0
        versionCode = (major * 10000) + (minor * 100) + patch
        versionName = "$major ($major.$minor.$patch)"
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file('keystore.properties')
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' [DEBUG]'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning', 'GoogleAppIndexingApiWarning' // app doesn't provide deep-links
        disable 'UnusedResources' //like "text_resultSmall"
    }

    android {
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // MVP
    implementation 'com.joaquimverges:helium:0.1.0'

    // widgets
    implementation 'com.github.guilhe:circular-progress-view:1.3.1'

    // Support libs
    def supportLib_version = '28.0.0-rc01'
    implementation "com.android.support:appcompat-v7:$supportLib_version"
    implementation "com.android.support:design:$supportLib_version"
    implementation "com.android.support:preference-v7:$supportLib_version"
    implementation "com.android.support:recyclerview-v7:$supportLib_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    // Room
    def room_version = '1.1.1'
    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // LeakCanary
    def leakcanary_version = '1.6.1'
    debugImplementation   "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
}
